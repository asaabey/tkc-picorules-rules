/*  Algorithm to generate CKD2 or 3, rapid progression (20% decline per annum)   */

 #define_ruleblock(tg4610,
    {
        description: "Algorithm to generate CKD2 or 3 rapid progression 20% decline per annum ",

        is_active:2

    }
);

#doc(,{
        txt:"Get CKD stage"
});



age => rout_dmg.age.val.bind();

dod => rout_dmg.dod.val.bind();

ckd => rout_ckd.ckd.val.bind();

#doc(,{
        txt:"previous CSU action and assumes that the trigger will never fire again"
});

csu_act => eadv.csu_action_tg4610._.lastdv();

dmg_source => rout_dmg_source.dmg_source.val.bind();

ckd_stage => rout_ckd.ckd_stage.val.bind();

cga_a_val => rout_ckd.cga_a_val.val.bind();

#doc(,{
        txt:"Get last l-1 and maximum where max is earlier than l-1"
});

egfr_l => eadv.lab_bld_egfr_c._.lastdv().where(dt > sysdate-365);

egfr_l1 => eadv.lab_bld_egfr_c._.lastdv().where(dt < egfr_l_dt and dt>egfr_l_dt - 180);

egfr_max => eadv.lab_bld_egfr_c._.maxldv().where(dt>sysdate-730 and dt < egfr_l1_dt);

#doc(,{
        txt:"Calc slope from max to last"
});

eb : {egfr_l_dt > egfr_max_dt => round((egfr_l_val-egfr_max_val)/((egfr_l_dt-egfr_max_dt)/365),2)};

#doc(,{
        txt:"Calc l and l-1 ratio to establish steady within 6 months accepting 20pct variance"
});


egfr_l_l1_qt : { coalesce(egfr_l1_val,0)>0 =>(egfr_l_val/egfr_l1_val)},{=>0};

egfr_ss : { egfr_l_l1_qt>0.8 and egfr_l_l1_qt<1.2 =>1 },{=>0};

#doc(,{
        txt:"Slope threshold for 1 and 2 is -20 per year for 3 onwards -30"
});

eb_thresh : {ckd<3 => -20},{ckd>=3 and ckd<6 => -30};


ckd_null : { nvl(ckd,0)=0 =>1},{=>0};

#doc(,{
        txt:"Exclude existing referred or reviewed"
});

ref_ren => rout_engmnt_renal.ref_renal.val.bind();

enc_ren => rout_engmnt_renal.enc_renal.val.bind();

#doc(,
    {
        txt:"Triggered for stage 1 or 2 with eb of minus 20pc provided no renal encounter",
        cite: "tg4610_ref1"
    }
);

#doc(,
    {
        txt:"only if slope x is more than 180 and egfr last value less than 80 and max is known"
    }
);

#doc(,
    {
        txt:"sensitivity adjustment with inclusion of a3 albuminuria"
    }
);
ex_flag : {dod!? or enc_ren=1 or ref_ren=1  or dmg_source=0 or age>69 or csu_act_dt!? or dmg_source=999=> 1},{=>0};

tg4610 : {
                ckd>0 and ckd<6 and nvl(eb,0)<eb_thresh
                and egfr_l_dt - egfr_max_dt >180
                and egfr_l_val<80 and egfr_max_val is not null
                and egfr_ss=1
                and cga_a_val>3
                and ex_flag=0 => 1
            },{=>0};

#define_attribute(
        tg4610,
        {
            label:"Alert:Unmanaged possible early CKD with rapid progression",
            desc:"Integer [0-1] if meets criteria ",
            is_reportable:1,
            is_trigger:1,
            type:2,
            priority:1
        }
    );
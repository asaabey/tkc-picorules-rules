        /* Rule block to Calculate uacr metrics */
        
          #define_ruleblock(ckd_uacr_metrics,
            {
                description: "Rule block to Calculate uacr metrics",
                is_active:2
                
            }
        );
        
        #doc(,
            {
                txt : "calculate uacr metrics"
            }
        );
        
       
        acr_l => eadv.lab_ua_acr._.lastdv();
        
        acr_max_l => eadv.lab_ua_acr._.maxldv();
        
        acr_3_n => eadv.lab_ua_acr.dt.count().where(val >3);
        
        acr_n => eadv.lab_ua_acr.dt.count();
        
        sigma_l_max : {  acr_l_dt> acr_max_l_dt and acr_max_l_val>0 => round(acr_l_val/acr_max_l_val,2)};
        
        sigma_3n_n : {  coalesce(acr_n,0)>0 => round(acr_3_n/acr_n,2)};
        
        acr_outdated : {sysdate-acr_l_dt > 730 =>1},{=>0};
        
        acr_past_pers_flag : { acr_l_val<3 and sigma_3n_n>0 and acr_3_n>1 => 1},{=>0};
        
        acr_past_singular_flag : {acr_l_val<3 and sigma_3n_n>0 and acr_3_n=1 =>1},{=>0};
        
        acr_decline_flag : {sigma_l_max<0.75 and acr_max_l_val>=3 =>1},{=>0};
        
        
        
        #doc(,{
                txt : "check for uACR persistence based on KDIGO persistence definition "
        });
        
        
        acr_1m_v3_n => eadv.lab_ua_acr.val.count().where(dt<acr_l_dt-90 and val>=3);
        
        /*
        affects performance
        
        acr_1m_v30_n => eadv.lab_ua_acr.val.count().where(dt<acr_l_dt-90 and val>=30);
        acr_1m_v300_n => eadv.lab_ua_acr.val.count().where(dt<acr_l_dt-30 and val>=300);
        acr_1m_v300_flag : { acr_1m_v300_n >0 => 1},{=>0};
        */
        
        a_pers : {coalesce(acr_1m_v3_n,0)>0 => 1},{=>0};
        
        #doc(,{
            txt : "check for uACR assumption violation"
        });
        
        u_leuc => eadv.[lab_ua_poc_leucocytes,lab_ua_leucocytes].dt.lastdv().where(dt > acr_l_dt-14 and dt < acr_l_dt+14);
        
        a_asm_viol_ex : { u_leuc_val=0 =>1},{=>0};
        
        #doc(,{
            txt : "uACR criteria not otherwise met"
        });
        
        acr_nom_crit : { a_asm_viol_ex=1 and (acr_past_pers_flag=1 or acr_past_singular_flag=1) =>1},{=>0};
        
        #doc(,{
                txt : "Apply KDIGO 2012 staging",
                cite: "ckd_ref1, ckd_ref2"
        });
        
            
        ckd_uacr_metrics: {acr_l_val<3 => 1},
                {acr_l_val<30 AND acr_l_val>=3 => 2},
                {acr_l_val<300 AND acr_l_val>=30 => 3},
                {acr_l_val>300 => 4},{=>0};
                
        /* #define_attribute(acr_1m_v300_flag, { label: "Albuminuria : Macroalbuminuria (300)", is_reportable: 1, type: 2 } ); */
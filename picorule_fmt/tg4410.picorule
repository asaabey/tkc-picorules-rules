        /* Algorithm to detect nephrotic syndrome */
        
         #define_ruleblock(tg4410,
            {
                description: "Algorithm to detect nephrotic syndrome",
                is_active:2
                
                
            }
        );
        
        
        #doc(,
            {
                txt:"Calculate information quantity"
            }
        );
        
        dod => rout_dmg.dod.val.bind();
        

        
        iq_uacr => eadv.lab_ua_acr.val.count(0).where(dt>sysdate-365);
        iq_egfr => eadv.lab_bld_egfr.val.count(0).where(dt>sysdate-365);
        iq_alb => eadv.lab_bld_albumin.val.count(0).where(dt>sysdate-365);
        iq_chol => eadv.lab_bld_cholesterol_total.val.count(0).where(dt>sysdate-365);
        iq_sbp => eadv.obs_bp_systolic.val.count(0).where(dt>sysdate-365);
        iq_ana => eadv.lab_bld_ana.val.count(0).where(dt>sysdate-(365*5));
        iq_spep => eadv.lab_bld_ana.val.count(0).where(dt>sysdate-(365*5));
        
        iq_tier :
                {iq_uacr>=2 and least(iq_egfr,iq_alb,iq_chol,iq_sbp,iq_ana,iq_spep)>1 => 4},
                {iq_uacr>=2 and least(iq_egfr,iq_alb,iq_chol,iq_sbp,0)>1 => 3},
                {iq_uacr>=2 and least(iq_egfr,iq_alb)>1 => 2},
                {iq_uacr>=2 and iq_alb>1 => 1},
                {=>0};
        
        #doc(,{
                txt:"previous CSU action and assumes that the trigger will never fire again"
        });  
        
        csu_act => eadv.csu_action_tg4410._.lastdv();
        
        dmg_source => rout_dmg_source.dmg_source.val.bind();
               
        rrt => rout_rrt.rrt.val.bind();
        dm => rout_cd_dm_dx.dm.val.bind();
        ckd => rout_ckd.ckd.val.bind();
       
        #doc(,
            {
                txt:"Exclude previously diagnosed nephrotic and if recent renal encounters"
            }
        );  
        
        
        
        dx_nephrotic => eadv.[icd_n04%].dt.count(0);
        
                
        ref_ren => rout_engmnt_renal.ref_renal.val.bind();
        
        enc_ren => rout_engmnt_renal.enc_renal.val.bind();
        

    
        ex_flag :{greatest(rrt,dm,enc_ren,dx_nephrotic)>0 or dod!? or ckd>4 or csu_act_dt!? or dmg_source=999 => 1},{=>0};

        
        #doc(,{
                txt:"Inclusions for nephrotic syndrome",
                cite: "tg4410_ref1, tg4410_ref2"
        });
        
        uacr_n => eadv.lab_ua_acr.dt.count(0).where(val>300 and dt>sysdate-365);
        
        uacr1 => eadv.lab_ua_acr.val.last().where(dt>sysdate-365);
        
        uacr2 => eadv.lab_ua_acr.val.last(1).where(dt>sysdate-365);
        
        #doc(,{
                txt:"Use delta of log transformed uacr" 
        });
       
        uacr_log_delta : {uacr1>0 and uacr2>0 => round(log(10,uacr1)-log(10,uacr2),1)};
        
        #doc(,{
                txt:"Nephrotic associations of albumin and cholesterol"
        });
        
        
        alb1 => eadv.lab_bld_albumin.val.last().where(dt>sysdate-365);
        
        alb2 => eadv.lab_bld_albumin.val.last(1).where(dt>sysdate-365);
        
        chol1 => eadv.lab_bld_cholesterol_total.val.last(1).where(dt>sysdate-365);
        
        
        
        low_alb : {nvl(alb1,0)<30=>1},{=>0};
        high_chol : {nvl(chol1,0)>7=>1},{=>0};
        
        
                
        tg4410 : {ex_flag=0 and uacr1>300 and uacr2>300 and uacr_log_delta>-0.1 => 1 },{=>0};
        
        #define_attribute(
            tg4410,
            {
                label:"Alert:Nephrotic range proteinuria in the absence of diabetes",
                desc:"Integer [0-1] if meets criteria ",
                is_reportable:1,
                is_trigger:1,
                type:2,
                priority:1
            }
        );
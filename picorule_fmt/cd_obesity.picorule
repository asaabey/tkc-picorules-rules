/* Ruleblock to assess obesity  */
        
        #define_ruleblock(cd_obesity,
            {
                description: "Ruleblock to assess obesity",
                is_active:2

            }
        );
        
        #doc(,{
                    txt: "Height in cm"
        });
            
        ht => eadv.obs_height.val.lastdv();
        
        ht_err : { ht_val<50 or ht_val>300 =>1},{=>0};
        
        #doc(,{
                    txt: "Weight in kg"
        });
        
        wt => eadv.obs_weight.val.lastdv();
        
        wt_err : { wt_val<10 or wt_val>300 =>1},{=>0};

        bmi_err : {ht_err=1 or wt_err=1 =>1},{=>0};
        
        bmi : { ht_err=0 and wt_err=0 => round(wt_val/power(ht_val/100,2),1) };
    
        
        obs_icd => eadv.[icd_e66%].dt.count(0);
        
        obs_icpc => eadv.[icpc_t82%].dt.count(0);
        
        #doc(,{
                    txt: "Obesity classification",
                    cite: "cd_obesity_ref2"
        });
        
        bmi_class : { bmi_err=0 and bmi between 30 and 34.9 => 1},
                    { bmi_err=0 and bmi between 35 and 39.9 => 2},
                    { bmi_err=0 and bmi >= 40 =>3},
                    {=>0};
        
        
        #doc(,
                {
                    txt: "Obesity diagnosis where BMI >30",
                    cite: "cd_obesity_ref1"
                }
            );
        
        cd_obesity : { bmi>30 => 1 },{=>0};
        
        obs_dx_uncoded : {bmi>30 and greatest(obs_icd,obs_icpc)=0 =>1},{=>0};
        
        #define_attribute(
            cd_obesity,
                {
                    label:"Obesity",
                    desc:"Integer [0-1] if Obesity based on code and observation criteria",
                    is_reportable:1,
                    type:2
                }
        );
/* Ruleblock to assess for dyslipidaemia*/

#define_ruleblock([[rb_id]],
    {
        description: "Ruleblock to assess for cd_dyslipidaemia",
        is_active:2

    }
);


ascvd => rout_cvra.cvd_prev.val.bind();

dob => rout_dmg.dob.val.bind();

gender => rout_dmg.gender.val.bind();


cad => rout_cd_cardiac_cad.cd_cardiac_cad.val.bind();

age : { dob!? => round((sysdate-dob)/365.25,1)};

prem_cad_wt :   { gender=0 and age<55 and cad=1 => 2},
                { gender=1 and age<60 and cad=1 => 2},
                {=>0};

prem_ascvd_wt :     { gender=0 and age<55 and ascvd=1 and cad=0 => 1},
                    { gender=1 and age<60 and ascvd=1 and cad=0 => 1},
                    {=>0};





dyslip_code_dt => eadv.[icd_e78%,icpc_t93%].dt.min();
tc => eadv.lab_bld_cholesterol_total._.lastdv().where(dt>sysdate-365);
hdl => eadv.lab_bld_cholesterol_hdl._.lastdv().where(dt>sysdate-365);

tg => eadv.lab_bld_cholesterol_tg._.lastdv().where(dt>sysdate-365);

ldl => eadv.lab_bld_cholesterol_ldl._.lastdv().where(dt>sysdate-365);

ldl_5_n => eadv.lab_bld_cholesterol_ldl.dt.count().where(val > 5 and dt > sysdate-1825);

ldl_unl : { ascvd=1 => 1.8},{=>4.9};

ldl_dls_wt :    { ldl_val>8.5 => 8},
                { ldl_val between 6.5 and 8.4 => 5},
                { ldl_val between 5.0 and 6.4 => 3},
                { ldl_val between 4.0 and 4.9 => 1},
                { =>0};

dls : { . => prem_cad_wt + ldl_dls_wt + prem_ascvd_wt };

fhc_prob :  { dls>8 =>4 },
            { dls between 6 and 8 =>3},
            { dls between 3 and 5 =>2},
            { dls<3 =>1},
            {=>0};


fhc_prob4 : { fhc_prob=4 =>1},{=>0};

fhc_prob3 : { fhc_prob=3 =>1},{=>0};

fhc_prob2 : { fhc_prob=2 =>1},{=>0};

fhc_prob1 : { fhc_prob=1 =>1},{=>0};

ldl_subopt : { (ldl_val/ldl_unl)>1.2 =>1},{=>0};

rx_statin_dt => eadv.[rxnc_c10aa].dt.last().where(val=1);
rx_ezet_dt => eadv.[rxnc_c10ax09].dt.last().where(val=1);
rx_evolocumab_dt => eadv.[rxnc_c10ax13].dt.last().where(val=1);

rx_statin : { rx_statin_dt!? =>1  },{=>0};
rx_ezet : { rx_ezet_dt!? =>1  },{=>0};
rx_evolocumab : {rx_evolocumab_dt!? =>1 },{=>0};


[[rb_id]] :  {((ascvd=1 and nvl(ldl_val,0)>1.8)) or nvl(ldl_val,0)>4.9 or dyslip_code_dt!? or fhc_prob>1 => 1},{=>0};


#define_attribute(
    fhc_prob4,
    {
        label: "Definitive Familial Hypercholesterolaemia based on modified DLS",
        desc: "Definitive Familial Hypercholesterolaemia based on modified DLS",
        is_reportable: 1,
        type: 1001
    }
);

#define_attribute(
    fhc_prob3,
    {
        label: "Probable Familial Hypercholesterolaemia based on modified DLS",
        desc: "Probable Familial Hypercholesterolaemia based on modified DLS",
        is_reportable: 1,
        type: 1001
    }
);

#define_attribute(
    fhc_prob2,
    {
        label: "Possible Familial Hypercholesterolaemia based on modified DLS",
        desc: "Possible Familial Hypercholesterolaemia based on modified DLS",
        is_reportable: 1,
        type: 1001
    }
);


#define_attribute(
    ldl_5_n,
    {
        label:"LDL gt 5 threshold",
        desc:"LDL gt 5 threshold",
        is_reportable:1,
        type:2
    }
);
#define_attribute(ldl_val,
{
        label:"LDL cholesterol concentration",
        desc:"LDL cholesterol concentration",
        is_reportable:1,
        type:2
});
#define_attribute(tc_val,
{
        label:"Total cholesterol concentration",
        desc:"Total cholesterol concentration",
        is_reportable:1,
        type:2
});
#define_attribute(hdl_val,
{
        label:"HDL cholesterol concentration",
        desc:"HDL cholesterol concentration",
        is_reportable:1,
        type:2
});
#define_attribute(tg_val,
{
        label:"Triglyceride concentration",
        desc:"Triglyceride concentration",
        is_reportable:1,
        type:2
});
#define_attribute(tc_dt,
{
        label:"Total cholesterol test date",
        desc:"Total cholesterol test date",
        is_reportable:1,
        type:12
});
#define_attribute(rx_statin,
{
        label:"Statin Prescribed",
        desc:"Statin Prescribed",
        is_reportable:1,
        type:1001
});
#define_attribute(rx_ezet,
{
        label:"Ezetimibe Prescribed",
        desc:"Ezetimibe Prescribed",
        is_reportable:1,
        type:1001
});
#define_attribute(rx_evolocumab,
{
        label:"Evolocumab Prescribed",
        desc:"Evolocumab Prescribed",
        is_reportable:1,
        type:1001
});
#define_attribute(ascvd,
{
        label:"Previous atherosclerotic cardio vascular disease",
        desc:"Previous atherosclerotic cardio vascular disease",
        is_reportable:1,
        type:1001
});
#define_attribute(
    [[rb_id]],
    {
        label: "Hypercholesterolaemia",
        desc: "Presence of Hypercholesterolaemia",
        is_reportable: 1,
        type: 1001
    }
);
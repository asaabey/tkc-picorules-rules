#define_ruleblock(cd_hep_b_sero,
    {
        description: "Algorithm to detect Chronic hepatitis B from serology",
        is_active: 2
    }
);

/* HepB Surface antibody and antigen */

s_ab_level => eadv.[lab_bld_hbs_ab_level]._.lastdv();

s_ab_val :
    { s_ab_level_val = 0 => 0 },
    { s_ab_level_val > 0 => 1 };

s_ab_dt :
    { . => s_ab_level_dt };

s_ag => eadv.[lab_bld_hbs_ag]._.lastdv();

/* HepB Core antibody */

c_ab => eadv.[lab_bld_hbc_ab]._.lastdv();

/* HepB e antibody and antigen */

e_ab_0 => eadv.[lab_bld_hbe_ab_level, lab_bld_hbe_ab]._.lastdv();

e_ag_0 => eadv.[lab_bld_hbe_ag_level, lab_bld_hbe_ag]._.lastdv();

e_ab :
    { e_ab_0_val=0 => 0 },
    { e_ab_0_val>0 => 1 };

e_ag :
    { e_ag_0_val=0 => 0 },
    { e_ag_0_val>0 => 1 };

/* HepB Viral Load */

vl => eadv.[lab_bld_hbv_viral_load]._.lastdv();

/* Liver Function AST */

ast => eadv.[lab_bld_ast]._.lastdv();

alt => eadv.[lab_bld_alt]._.lastdv();

/*
+--------------------------------------------------------------------------------------------------------------------------------------------+
| Hepatitis B virus testing and interpreting test results                                                                                    |
| https:\\hepatitisb.org.au/hepatitis-b-virus-testing-and-interpreting-test-results/                                                         |
+----------+----------+----------+--------------+------------------------------------------+----------+--------------------------------------+
| HBsAg    | anti-HBc | Anti-HBs | IgM anti-HBc | Interpretation                           | Status # |            Status Label              |
| s_ag_val | c_ab_val | s_ab_val | c_ab_igm_val |                                          |          |                                      |
+----------+----------+----------+--------------+------------------------------------------+----------+--------------------------------------+
| pos      | pos      | neg      | neg          | Chronic HBV infection                    |        1 | HepB Positive: Chronic Infection *1  |
| pos      | pos      | neg      | pos          | Acute HBV infection                      |        2 | HepB Positive: Acute Infection *1    |
| neg      | neg      | neg      |              | Susceptible to infection                 |        3 | HepB Negative: Non Immune            |
| neg      | pos      | pos      |              | Immune due to resolved infection         |        4 | HepB Negative: Immune by Exposure    |
| neg      | neg      | pos      |              | Immune due to hepatitis B vaccination    |        5 | HepB Negative: Immune by Vaccination |
| neg      | pos      | neg      |              | Isolated elevation of anti-HBc           |        6 | HepB Positive: Chronic Infection NOS |
+----------+----------+----------+--------------+------------------------------------------+----------+--------------------------------------+
| pos      | pos      | pos      |              | No Interpretation                        |        7 |                                      |
| pos      | neg      | pos      |              | No Interpretation                        |        8 |                                      |
| pos      | neg      | neg      |              | No Interpretation                        |        9 |                                      |
+----------+----------+----------+--------------+------------------------------------------+----------+--------------------------------------+
| *1: Without IgM data we can't distinguish between acute and chronic infection so we will assume chronic                                    |
+--------------------------------------------------------------------------------------------------------------------------------------------|
*/

hbv_status :
    { s_ag_val=1 and c_ab_val=1 and s_ab_val=0 => 1 },
    { s_ag_val=0 and c_ab_val=0 and s_ab_val=0 => 3 },
    { s_ag_val=0 and c_ab_val=1 and s_ab_val=1 => 4 },
    { s_ag_val=0 and c_ab_val=0 and s_ab_val=1 => 5 },
    { s_ag_val=0 and c_ab_val=1 and s_ab_val=0 => 6 },
    { s_ag_val=1 and c_ab_val=1 and s_ab_val=1 => 7 },
    { s_ag_val=1 and c_ab_val=0 and s_ab_val=1 => 8 },
    { s_ag_val=1 and c_ab_val=0 and s_ab_val=0 => 9 };

/*
    { s_ag_val=1 and c_ab_val=1 and s_ab_val=0 and c_ab_igm_val=0 => 1 }
    { s_ag_val=1 and c_ab_val=1 and s_ab_val=0 and c_ab_igm_val=1 => 2 }
*/

hbv_status_date_upper_dt :
    { hbv_status!? => greatest_date(s_ag_dt, c_ab_dt, s_ab_dt) };

hbv_status_date_lower_dt :
    { hbv_status!? => least_date(s_ag_dt, c_ab_dt, s_ab_dt) };

hbv_status_date_spread :
    { hbv_status!? => hbv_status_date_upper_dt - hbv_status_date_lower_dt };

hbv_status_lbl :
    { hbv_status=1 => `HepB Positive: Chronic Infection` },
    { hbv_status=2 => `HepB Positive: Acute Infection` },
    { hbv_status=3 => `HepB Negative: Non Immune` },
    { hbv_status=4 => `HepB Negative: Immune by Exposure` },
    { hbv_status=5 => `HepB Negative: Immune by Vaccination` },
    { hbv_status=6 => `HepB Positive: Chronic Infection NOS` },
    { hbv_status=7 => `Uninterpreted Serological Results: +++` },
    { hbv_status=8 => `Uninterpreted Serological Results: +-+` },
    { hbv_status=9 => `Uninterpreted Serological Results: +--` };

/*
+--------------------------+----------------------------------+
| Hep B Virus Phase        |                                  |
+-------+------------------+----------------------------------+
| Value |      Label       | Modern Label                     |
+-------+------------------+----------------------------------+
|     1 | Immune tolerance | HBeAg-positive chronic infection |
|     2 | Immune clearance | HBeAg-positive chronic hepatitis |
|     3 | Immune control   | HBeAg-negative chronic infection |
|     4 | Immune escape    | HBeAg-negative chronic hepatitis |
+-------+------------------+----------------------------------+
*/


/*
ALT and HBV VL are ok as long as within 15 months of each other
eAg and eAb can just be the most recent available
*/

alt_vs_vl_date_spread :
    { alt_dt  > vl_dt => alt_dt - vl_dt },
    { alt_dt <= vl_dt => vl_dt - alt_dt };

alt_vs_vl_date_spread_ok :
    { alt_vs_vl_date_spread < (15 * 30) => 1 },
    { . => null };

gender => rout_dmg.gender._.bind();

alt_uln :
    { gender = 1 => 30 },
    { gender = 0 => 19 };

alt_is_normal :
    { alt_val <= alt_uln => 1 },
    { alt_val >  alt_uln => 0 };

alt_is_elevated :
    { alt_is_normal = 0 => 1 },
    { alt_is_normal = 1 => 0 };

vl_bucket :
    {                        vl_val <     2000 => '0-LOW'      },
    { vl_val >=     2000 and vl_val <    20000 => '1-HIGH2K'   },
    { vl_val >=    20000 and vl_val <  1000000 => '2-HIGH20K'  },
    { vl_val >=  1000000 and vl_val < 10000000 => '3-HIGH10^6' },
    { vl_val >= 10000000                       => '4-HIGH10^7' };

/*
https:\\ashm.org.au/wp-content/uploads/2023/08/ASHM_Decision-Making-in-Hepatitis-B-Toolkit-Update_Nov.pdf
*/

hbv_phase_v1_tmp :
    { e_ag = 0 and e_ab = 1 and alt_is_elevated = 1 and vl_val >    2000 => 4 },
    { e_ag = 0 and e_ab = 1 and alt_is_elevated = 0 and vl_val <    2000 => 3 },
    { e_ag = 1              and alt_is_elevated = 1 and vl_val >   20000 => 2 },
    { e_ag = 1              and alt_is_elevated = 0 and vl_val > 1000000 => 1 };

hbv_phase : { hbv_status = 1 and alt_vs_vl_date_spread_ok = 1 => hbv_phase_v1_tmp };

/*
hbv_phase_v2 is based on the following Example Stata code:
    gen phase=1 if hbsag0==1 & hbeag0==1
    replace phase=2 if phase==1 & sex==0 & alt0>=19 & alt0!=. & hbvdna0>=2000 & hbvdna0!=.
    replace phase=2 if phase==1 & sex==1 & alt0>=30 & alt0!=. & hbvdna0>=2000 & hbvdna0!=.
    replace phase=3 if hbsag0==1 & hbeag0==0
    replace phase=4 if phase==3 & hbvdna0>=2000 & hbvdna0!=.
*/

hbv_phase_v2_tmp :
    { e_ag = 0 and s_ag_val = 1 and vl_val >= 2000                         => 4 },
    { e_ag = 0 and s_ag_val = 1                                            => 3 },
    { e_ag = 1 and s_ag_val = 1 and vl_val >= 2000 and alt_is_elevated = 1 => 2 },
    { e_ag = 1 and s_ag_val = 1                                            => 1 };

hbv_phase_v2 :
    { hbv_status = 1 and alt_vs_vl_date_spread_ok = 1 => hbv_phase_v2_tmp };

/*
Australian consensus recommendations for the management of hepatitis B infection
https:\\www.gesa.org.au/public/13/files/Education%20%26%20Resources/Clinical%20Practice%20Resources/Hep%20B/HBV%20consensus%20Mar%202022%20Updated.pdf
Page 21-28

5.2.3.1 Phase I: immune tolerant (HBeAg-positive chronic infection)
    e_ag=1
    vl_val >= 20,000
    alt <= ULN (19/30)

5.2.3.2 Phase II: immune clearance (HBeAg-positive chronic hepatitis)
    e_ag=1
    vl_val >= ? (HBV DNA levels remain high, where is high defined?) should we use 20,000 or 10^6 or 10^7
    alt > ULN (19/30)

5.2.3.3 Phase III: immune control (HBeAg-negative chronic infection)
    e_ag=0
    vl_val < 2,000
    alt <= ULN (19/30)
    e_ab = 1

5.2.3.4 Phase IV: immune escape (HBeAg-negative chronic hepatitis)
    e_ag=0
    vl_val > 2,000
    alt > ULN (19/30)
    e_ab = 1
*/

hbv_phase_v3_tmp :
    { e_ag = 0 and e_ab = 1 and alt_is_elevated = 1 and vl_val >=  2000 => 4 },
    { e_ag = 0 and e_ab = 1 and alt_is_elevated = 0 and vl_val <   2000 => 3 },
    { e_ag = 1              and alt_is_elevated = 1 and vl_val >= 20000 => 2 },
    { e_ag = 1              and alt_is_elevated = 0 and vl_val >= 20000 => 1 };

hbv_phase_v3 :
    { hbv_status = 1 and alt_vs_vl_date_spread_ok = 1 => hbv_phase_v3_tmp };

hbv_phase_date_upper_dt :
    { hbv_phase!? => greatest_date(e_ag_0_dt, e_ab_0_dt, alt_dt, vl_dt) };

hbv_phase_date_lower_dt :
    { hbv_phase!? => least_date(e_ag_0_dt, e_ab_0_dt, alt_dt, vl_dt) };

hbv_phase_date_spread :
    { hbv_phase!? => hbv_phase_date_upper_dt - hbv_phase_date_lower_dt };

hbv_phase_lbl :
    { hbv_phase = 1 => `Immune tolerance phase` },
    { hbv_phase = 2 => `Immune clearance phase` },
    { hbv_phase = 3 => `Immune control phase` },
    { hbv_phase = 4 => `Immune escape phase` };

hbv_phase_v2_lbl :
    { hbv_phase_v2 = 1 => `Immune tolerance phase` },
    { hbv_phase_v2 = 2 => `Immune clearance phase` },
    { hbv_phase_v2 = 3 => `Immune control phase` },
    { hbv_phase_v2 = 4 => `Immune escape phase` };

hbv_phase_v3_lbl :
    { hbv_phase_v3 = 1 => `Immune tolerance phase` },
    { hbv_phase_v3 = 2 => `Immune clearance phase` },
    { hbv_phase_v3 = 3 => `Immune control phase` },
    { hbv_phase_v3 = 4 => `Immune escape phase` };

cd_hep_b_sero :
    { hbv_status in (1,6) => 1 },
    { => 0 };
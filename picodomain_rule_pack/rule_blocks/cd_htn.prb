/* Algorithm to assess hypertension  */

#define_ruleblock([[rb_id]],
    {
        description: "Algorithm to assess hypertension",
        is_active:2

    }
);

#doc(,
    {
        txt:"Calculate information entropy"
    }
);


iq_sbp => eadv.obs_bp_systolic.val.count(0).where(dt>sysdate-730);

iq_tier : {iq_sbp>1 => 2},{iq_sbp>0 => 1},{=>0};

#doc(,
    {
        section: "Diagnosis",
    }
);

#doc(,
    {
        txt:"Hypertension diagnosis: code criteria"
    }
);



htn_icd => eadv.[icd_i10%,icd_i15%].dt.min();
htn_icpc => eadv.[icpc_k85%,icpc_k86%,icpc_k87%].dt.min();

#doc(,
    {
        txt:"Hypertension diagnosis: observation criteria at least 3 readings over SBP140 within 2 years",
        cite:"htn_nhf_2016,htn_aha_2018,htn_mja_2016"
    }
);

htn_obs => eadv.obs_bp_systolic.val.count(0).where(val>140 and dt>sysdate-730);

#doc(,
    {
        txt:"Ancillary information for causality"
    }
);


bld_k_val => eadv.lab_bld_potassium.val.last().where(dt>sysdate-730);

bld_k_state : {nvl(bld_k_val,0)>5.2 =>3},{nvl(bld_k_val,0)>4.0 =>2},{=>1};

#doc(,
    {
        txt:"Hypertension diagnosis: treatment criteria based on RxNorm medication codes"
    }
);

htn_rxn_acei => eadv.[rxnc_c09aa].dt.count(0).where(val=1);
htn_rxn_arb => eadv.[rxnc_c09ca].dt.count(0).where(val=1);
htn_rxn_bb => eadv.[rxnc_c07%].dt.count(0).where(val=1);
htn_rxn_ccb => eadv.[rxnc_c08%].dt.count(0).where(val=1);
htn_rxn_c02 => eadv.[rxnc_c02%].dt.count(0).where(val=1);
htn_rxn_diuretic_thiaz => eadv.[rxnc_c03aa].dt.count(0).where(val=1);
htn_rxn_diuretic_loop => eadv.[rxnc_c03c%].dt.count(0).where(val=1);

htn_rxn_raas : { greatest(htn_rxn_acei,htn_rxn_arb)>0 =>1},{=>0};

htn_rxn : { coalesce(htn_rxn_acei, htn_rxn_arb, htn_rxn_bb, htn_rxn_ccb , htn_rxn_c02 , htn_rxn_diuretic_thiaz , htn_rxn_diuretic_loop) is not null =>1 },{=>0};

#doc(,
    {
        section: "Complications"
    }
);


#doc(,
    {
        txt:"Hypertension diagnosis: vintage or date of onset"
    }
);




htn_fd_code : { . => least_date(htn_icd,htn_icpc)};

htn_fd_obs => eadv.obs_bp_systolic.dt.min().where(val>140);


htn_fd : { .=> least_date(htn_fd_code,htn_fd_obs)};

htn_fd_yr : { htn_fd!? => to_char(htn_fd,`YYYY`) };

htn_type_2 => eadv.[icd_i15%].dt.count(0);

htn_from_obs : { htn_fd_obs<htn_fd_code =>1},{htn_fd_obs!? and htn_fd_code? =>1},{=>0};

#doc(,
    {
        txt:"Hypertension chronology"
    }
);


htn_vintage_yr_ : { htn_fd is not null => round((sysdate-htn_fd)/365,0)},{=>0};

htn_vintage_cat : { htn_vintage_yr_>=0 and htn_vintage_yr_ <10 => 1 },
                    { htn_vintage_yr_>=10 and htn_vintage_yr_ <20 => 2 },
                    { htn_vintage_yr_>=20=> 3 },{=>0};


#doc(,
    {
        section: "Management"
    }
);



htn : {coalesce(htn_icd,htn_icpc)!? or htn_obs>2 =>1},{=>0};

htn_prev : { htn_fd!? => 1 },{=>0};

htn_incd : { htn_fd > sysdate - 365 => 1},{=>0};

[[rb_id]] : {.=> htn};

htn_dx_uncoded : {htn_obs>=3 and coalesce(htn_icd,htn_icpc)? => 1},{=>0};

#define_attribute(
    [[rb_id]],
    {
        label:"Hypertension",
        desc:"Presence of Hypertension",
        is_reportable:1,
        type:2
    }
);

#define_attribute(
    htn_incd,
    {
        label:"Incident Hypertension",
        desc:"Presence of Hypertension",
        is_reportable:1,
        type:2
    }
);

#define_attribute(
    htn_prev,
    {
        label:"Prevalent Hypertension",
        desc:"Presence of Hypertension",
        is_reportable:1,
        type:2
    }
);

#define_attribute(htn_rxn_acei,{label:"treated with ACEi",is_reportable:1,type:2});
#define_attribute(htn_rxn_arb,{label:"treated with ARB",is_reportable:1,type:2});
#define_attribute(htn_rxn_ccb,{label:"treated with Calcium channel blocker",is_reportable:1,type:2});
#define_attribute(htn_rxn_bb,{label:"treated with Beta-blocker",is_reportable:1,type:2});
#define_attribute(htn_rxn_c02,{label:"treated with Centrally acting agent",is_reportable:1,type:2});
#define_attribute(htn_rxn_diuretic_thiaz,{label:"treated with thiazide diuretic",is_reportable:1,type:2});
#define_attribute(htn_rxn_diuretic_loop,{label:"treated with loop diuretic",is_reportable:1,type:2});